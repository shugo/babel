class MAIN is
   repeat!(once n: INT) is
      i ::= 0;
      loop
         while!(i < n);
         yield;
         i := i + 1;
      end;
   end;

   upto!(once from, once to: INT): INT is
      i ::= from;
      loop
         while!(i <= to);
         yield i;
         i := i + 1;
      end;
   end;

   sum!(i: INT): INT is
      r ::= 0;
      loop
         r := r + i;
         yield r;
      end;
   end;

   product!(i: INT): INT is
      r ::= 1;
      loop
         r := r * i;
         yield r;
      end;
   end;

   main is
      loop
         repeat!(3);
         #OUT + "hello\n";
      end;
      sum: INT;
      product: INT;
      loop
         i ::= upto!(1, 10);
         #OUT + "i=" + i + "\n";
         sum := sum!(i);
         product := product!(i);
      end;
      #OUT + "sum=" + sum + "\n";
      #OUT + "product=" + product + "\n";
   end;
end;
